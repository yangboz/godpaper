<?xml version="1.0" encoding="utf-8"?>
<s:Application xmlns:fx="http://ns.adobe.com/mxml/2009" 
			   xmlns:s="library://ns.adobe.com/flex/spark" 
			   xmlns:mx="library://ns.adobe.com/flex/mx" applicationComplete="connect()">
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>

	<fx:Script>
		<![CDATA[
			//(codename)Stratus
			private const SERVER:String = "rtmfp://stratus.adobe.com/";
			private const DEVKEY:String = "99ead580edaf280c060675f9-f614dd07a932";
			//(codename) Cirrus developer key is:
			//40a1c5b634bc4f531ad7757f-2e3cf422214e

			//To connect to the Cirrus service, open an RTMFP NetConnection to:
			//rtmfp://p2p.rtmfp.net/40a1c5b634bc4f531ad7757f-2e3cf422214e/

			private var nc:NetConnection;
			private var netGroup:NetGroup;

			[Bindable]
			private var user:String;

			[Bindable]
			private var connected:Boolean = false;

			private function connect():void{
				nc = new NetConnection();
				nc.addEventListener(NetStatusEvent.NET_STATUS,netStatus);
				nc.connect(SERVER+DEVKEY);	
			}

			private function netStatus(event:NetStatusEvent):void{
				write(event.info.code);

				switch(event.info.code){
					case "NetConnection.Connect.Success":
						setupGroup()
						break;

					case "NetGroup.Connect.Success":
						connected = true;

						break;

					case "NetGroup.Posting.Notify":
						receiveMessage(event.info.message);
						break;
				}
			}

			private function setupGroup():void{
				var groupspec:GroupSpecifier = new GroupSpecifier("myGroup/g1");
				groupspec.serverChannelEnabled = true;
				groupspec.postingEnabled = true;

				trace("Groupspec: "+groupspec.groupspecWithAuthorizations());

				netGroup = new NetGroup(nc,groupspec.groupspecWithAuthorizations());
				netGroup.addEventListener(NetStatusEvent.NET_STATUS,netStatus);


				user = "user"+Math.round(Math.random()*10000);
			}

			private var sequence:uint = 0;

			private function sendMessage():void{

				var message:Object = new Object();
				message.sender = netGroup.convertPeerIDToGroupAddress(nc.nearID);
				//message.sequence = sequence++;
				message.user = txtUser.text;
				message.text = txtMessage.text;


				netGroup.post(message);
				receiveMessage(message);

				txtMessage.text = "";
			}

			private function receiveMessage(message:Object):void{
				write(message.user+": "+message.text);
			}

			private function write(txt:String):void{
				txtHistory.text += txt+"\n";
			}


		]]>
	</fx:Script>
	<s:TextArea left="10" right="10" top="10" bottom="40" id="txtHistory"/>
	<s:TextInput x="10" id="txtUser" text="{user}" bottom="10"/>
	<s:TextInput left="145" right="88" id="txtMessage" bottom="10" enter="sendMessage()"/>
	<s:Button label="Send" click="sendMessage()" enabled="{connected}" bottom="10" right="10"/>

</s:Application>
